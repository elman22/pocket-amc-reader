// This file is part of Pocket AMC Reader.
// Copyright Â© 2010-2020 Elman <holdingscythe@zoznam.sk>
//
// Pocket AMC Reader is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// Pocket AMC Reader is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with Pocket AMC Reader.  If not, see <http://www.gnu.org/licenses/>.

apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'
    defaultConfig {
        applicationId "com.holdingscythe.pocketamcreader"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 104
        versionName '2.3.1'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            zipAlignEnabled true
            buildConfigField "int", "LOGLEVEL", "6"
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            zipAlignEnabled true
            buildConfigField "int", "LOGLEVEL", "2"
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = new File(
                    output.outputFile.name.replace(".apk", "-${variant.versionName}.apk"))
        }
    }
    lintOptions {
        abortOnError false
    }
    productFlavors {
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

if (project.hasProperty("signingProperties") && new File(project.property("signingProperties")).exists()) {

    Properties keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(rootProject.file(project.property("signingProperties"))))

    android {
        signingConfigs {
            production {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
        buildTypes {
            release {
                versionNameSuffix '.signed'
                signingConfig signingConfigs.production
            }
        }
    }
}

repositories {
    jcenter()
    google()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // Android material design
    implementation 'com.google.android.material:material:1.2.1'
    // https://github.com/spacecowboy/NoNonsense-FilePicker
    implementation 'com.nononsenseapps:filepicker:4.2.1'
    // https://github.com/Manabu-GT/ExpandableTextView
    implementation 'com.ms-square:expandableTextView:0.1.4'
    // https://github.com/nostra13/Android-Universal-Image-Loader
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    // https://github.com/InflationX/Calligraphy
    implementation 'io.github.inflationx:calligraphy3:3.1.1'
    implementation 'io.github.inflationx:viewpump:2.0.3'
    // https://github.com/woxblom/DragListView
    implementation 'com.github.woxthebox:draglistview:1.7.2'
    // https://github.com/timusus/RecyclerView-FastScroll
    implementation 'com.simplecityapps:recyclerview-fastscroll:2.0.1'
}