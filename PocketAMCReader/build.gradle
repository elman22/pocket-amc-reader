// This file is part of Pocket AMC Reader.
// Copyright Â© 2010-2017 Elman <holdingscythe@zoznam.sk>
//
// Pocket AMC Reader is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// Pocket AMC Reader is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with Pocket AMC Reader.  If not, see <http://www.gnu.org/licenses/>.

apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'
    defaultConfig {
        applicationId "com.holdingscythe.pocketamcreader"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 89
        versionName '2.1'
    }
    buildTypes {
        release {
            // minifyEnabled doesn't work, so it is disabled for now
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            zipAlignEnabled true
            buildConfigField "int", "LOGLEVEL", "6"
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            zipAlignEnabled true
            buildConfigField "int", "LOGLEVEL", "2"
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace(".apk", "-${variant.versionName}.apk"))
        }
    }
    lintOptions {
        abortOnError false
    }
    productFlavors {
    }
}

if (project.hasProperty("signingProperties") && new File(project.property("signingProperties")).exists()) {

    Properties keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(rootProject.file(project.property("signingProperties"))))

    android {
        signingConfigs {
            production {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
        buildTypes {
            release {
                versionNameSuffix '.signed'
                signingConfig signingConfigs.production
            }
        }
    }
}

repositories {
    jcenter()
}

ext {
    // versions for libraries
    supportLibVersion = '26.0.0-alpha1'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // Android support
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    // https://github.com/spacecowboy/NoNonsense-FilePicker
    compile 'com.nononsenseapps:filepicker:4.1.0'
    // https://github.com/Manabu-GT/ExpandableTextView
    compile 'com.ms-square:expandableTextView:0.1.4'
    // https://github.com/nostra13/Android-Universal-Image-Loader
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    // https://github.com/chrisjenx/Calligraphy
    compile 'uk.co.chrisjenx:calligraphy:2.3.0'
    // https://github.com/ksoichiro/Android-ObservableScrollView
    compile 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    // https://github.com/woxblom/DragListView
    compile 'com.github.woxthebox:draglistview:1.4.4'
}